# EVM 실습

![evm-practice](https://github.com/bohyunkang/road-to-bangkok/assets/65386533/bfc1975b-6017-41e7-9aa2-577986a74a98)

## 들어가며

앞선 모듈에서 살펴봤듯이 **EVM은 스마트컨트랙트 배포 및 실행을 처리하는 이더리움의 일부**입니다.
이더리움은 애플리케이션을 실행하는 기본으로 튜링 완전한 가상 머신을 제공하는데, 이를 이더리움 가상 머신(EVM)이라고 합니다.
솔리디티 등으로 개발한 스마트 컨트랙트는 코드 작성 후 EVM에서 실행할 수 있는 상태인 바이트코드로 변환해 이더리움 가상 머신에서 실행하는 것입니다.

튜링 머신은 우리가 사용하는 컴퓨터 모델을 수학적으로 단순화한 것입니다. 어떤 기록된 상태를 읽고 그에 맞게 동작시켜 새로운 상태를 기록합니다. 튜링 머신으로 해결할 수 있는 문제라면 프로그래밍 언어로 구현할 수 있는 상태입니다(실제로는 높은 성능을 내려고 복잡한 처리를 구현합니다). 이를 '튜링 완전(Turing Completeness)'이라고 합니다.

이더리움 가상 머신은 튜링 완전하므로 처리 속도, 데이터 크기, 성능상의 제약 등의 문제가 있더라도 어떤 프로그램이든 실행할 수 있습니다. 또한 이더리움 가상 머신은 샌드박스화되어 있어 프로그램 코드가 네트워크나 파일 시스템 등의 프로세스에 접근할 수 없습니다. 스마트 컨트랙트 역시 다른 스마트 컨트랙트에 접근이 제한됩니다.

이더리움은 처리 속도, 데이터 크기, 스펙상의 제약 등과 관계없이 어떤 어플리케이션이든 실행할 수 있다는 것을 보장합니다.
이번 모듈에서는 이러한 이더리움 가상 머신(이하 EVM)을 실질적인 코드를 작성해봄으로써 더 깊게 알아보려고 합니다.

## 사전 준비

이번 실습을 진행하기 전에 준비할 준비물은 아래 두 가지입니다.

1. [Remix IDE](https://remix.ethereum.org/) 준비
   - 참고: [Remix IDE 기본 사용법 알아보기](https://velog.io/@haerong22/Remix-IDE-기본-사용법)
2. [솔리디티(Solidity)](https://docs.soliditylang.org/) 및 스마트 컨트랙트에 대한 기본 이해

이번 실습에서는 [Remix IDE](https://remix.ethereum.org/)를 사용하여 코드 작성을 진행합니다. [솔리디티 베이직](https://github.com/Ludium-Official/road-to-bangkok) 모듈에서 솔리디티에 대한 자세한 문법들과 내용은 다룰 예정이므로, 이번 실습에서는 솔리디티 깊이 다루지는 않습니다. 간단하게 실습을 할 수 있는 정도로만 배워보도록 하겠습니다.

## 실습 1. EVM 바이트코드 생성하기

> 실습 목표: Remix IDE를 통해 간단한 Solidity 코드를 작성하여 EVM 바이트코드를 생성해본다.

이제 바이트코드 생성하는 실습을 진행해봅시다.

앞서 설명하였듯이 개발자가 이해할 수 있는 언어인 솔리디티 등으로 스마트 컨트랙트 코드를 작성하고, EVM에서 컴퓨터가 이해할 수있는 상태인 바이트코드로 변환하는 과정입니다.

### 1단계: Remix IDE 열기

1. 웹 브라우저를 열고 [Remix IDE](https://remix.ethereum.org/)로 이동합니다.

![1-1](https://github.com/bohyunkang/road-to-bangkok/assets/65386533/7685037e-4e4f-4958-a9e5-43e6e9001dc1)

### 2단계: 새 솔리디티 파일 만들기

1. 왼쪽 파일 탐색기 패널의 contracts 폴더에서 "Create new file" 버튼을 클릭하여 새 파일을 만듭니다.

<img width="1440" alt="1-2" src="https://github.com/bohyunkang/road-to-bangkok/assets/65386533/a5f58012-3f72-4125-9822-f5c705dac49b">

2. 파일 이름을 `MakeByteCode.sol`로 지정합니다.

### 3단계: MakeByteCode.sol 작성하기

![1-3](https://github.com/bohyunkang/road-to-bangkok/assets/65386533/74a509ff-748e-4dfb-b04b-b2987a60a280)

우선 `MakeByteCode`라는 빈 컨트랙트를 간단하게 작성합니다.

```solidity
  // SPDX-License-Identifier: MIT
  pragma solidity ^0.8.20;

  contract MakeByteCode {}
```

### 4단계: 컨트랙트 컴파일하기

이제 이 컨트랙트를 솔리디티 컴파일러(Solidity Compiler)를 통해 바이트 코드로 생성해보겠습니다.

1. 왼쪽 패널 위에서 세 번째 아이콘 중 Solidity Compiler 아이콘을 클릭합니다.
2. "`MakeByteCode.sol` Compile" 버튼을 클릭합니다.

<img width="1440" alt="1-4" src="https://github.com/bohyunkang/road-to-bangkok/assets/65386533/c29f7bef-6440-46b9-a1fe-cc645b45a654">

### 5단계: 생성된 바이트코드 확인하기

![1-6](https://github.com/bohyunkang/road-to-bangkok/assets/65386533/47896286-dd95-43d7-80e0-cffa5b21ce89)
그럼 `contracts > artifacts > build-info` 폴더에 JSON 파일이 생성되어 있는 것을 확인할 수 있습니다.
이 JSON 파일 속 output이라는 키값을 가진 객체 내부에 evm.bytecode.object에 해당 바이트코드가 생성되어 있는 것을 확인할 수 있습니다.

```
6080604052348015600e575f80fd5b50603e80601a5f395ff3fe60806040525f80fdfea264697066735822122055a1ec939d0ecaf4480b41eed03d5f1ec120e8d8fb281f55cb054cc3f602bdc164736f6c634300081a0033
```

이 바이트코드는 EVM에 의해 실행되는 하위 수준 명령을 나타냅니다.

## 실습 2. 상태 데이터 저장하기

> 실습 목표: EVM에서 스택 기반의 데이터 저장 방식을 이해하고, LIFO 방식으로 ADD, SUB, POP, PUSH 연산의 동작 원리를 학습한다.

### 1단계: Remix IDE에서 `StackOperations.sol` 파일 생성 및 코드 작성

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract StackOperations {
    uint256[] private stack;

    event Pushed(uint256 value);
    event Popped(uint256 value);
    event Added(uint256 a, uint256 b, uint256 result);
    event Subtracted(uint256 a, uint256 b, uint256 result);

    // PUSH 연산: 스택에 값을 추가
    function push(uint256 value) public {
        stack.push(value);
        emit Pushed(value);
    }

    // POP 연산: 스택에서 값을 제거하고 반환
    function pop() public returns (uint256) {
        require(stack.length > 0, "Stack underflow");
        uint256 value = stack[stack.length - 1];
        stack.pop();
        emit Popped(value);
        return value;
    }

    // ADD 연산: 스택에서 두 값을 꺼내 더한 후 결과를 스택에 저장
    function add() public {
        require(stack.length >= 2, "Not enough values on stack");
        uint256 a = pop();
        uint256 b = pop();
        uint256 result = a + b;
        push(result);
        emit Added(a, b, result);
    }

    // SUB 연산: 스택에서 두 값을 꺼내 뺀 후 결과를 스택에 저장
    function sub() public {
        require(stack.length >= 2, "Not enough values on stack");
        uint256 a = pop();
        uint256 b = pop();
        uint256 result = a - b;
        push(result);
        emit Subtracted(a, b, result);
    }

    // 스택의 현재 상태를 반환
    function getStack() public view returns (uint256[] memory) {
        return stack;
    }
}
```

- **PUSH 연산**:
  - `push(uint256 value)` 함수는 입력받은 `value`를 스택에 추가합니다.
  - 스택에 추가된 값은 `Pushed` 이벤트를 통해 기록됩니다.
- **POP 연산**:
  - `pop()` 함수는 스택의 마지막 값을 제거하고 반환합니다.
  - 스택에서 제거된 값은 `Popped` 이벤트를 통해 기록됩니다.
  - 스택이 비어 있을 경우, "Stack underflow" 에러를 발생시킵니다.
- **ADD 연산**:
  - `add()` 함수는 스택에서 두 값을 꺼내 더한 후 결과를 다시 스택에 저장합니다.
  - 더한 결과는 `Added` 이벤트를 통해 기록됩니다.
  - 스택에 두 개 이상의 값이 있어야 합니다.
- **SUB 연산**:
  - `sub()` 함수는 스택에서 두 값을 꺼내 뺀 후 결과를 다시 스택에 저장합니다.
  - 뺀 결과는 `Subtracted` 이벤트를 통해 기록됩니다.
  - 스택에 두 개 이상의 값이 있어야 합니다.

### 2단계: `StackOperations.sol` 컨트랙트 컴파일하기

### 3단계: `StackOperations.sol` 배포 후, 각 함수 호출하여 동작 확인하기

1. 각 함수(`push`, `pop`, `add`, `sub`)를 호출하여 스택의 동작을 확인합니다.
2. `getStack` 함수를 호출하여 현재 스택의 상태를 확인합니다.

## 실습 3. 옵코드 추가 연산하기

> 실습 목표: MOD, LT, BYTE 등의 추가적인 옵코드를 실습함으로써 옵코드를 더 깊게 이해한다.

### 1단계. Remix IDE에서 `OpcodeSample.sol` 파일 생성 및 코드 작성

먼저, 간단한 Solidity 컨트랙트를 작성합니다. 이 컨트랙트는 EVM에서 MOD, LT, BYTE 명령어를 사용하는 예제를 포함합니다.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract OpcodeSample {
    // MOD: 두 숫자의 나머지를 구하는 함수
    function modExample(uint256 a, uint256 b) public pure returns (uint256) {
        require(b != 0, "Divider cannot be zero");
        return a % b;
    }

    // LT: 두 숫자를 비교하는 함수
    function ltExample(uint256 a, uint256 b) public pure returns (bool) {
        return a < b;
    }

    // BYTE: 주어진 바이트 배열에서 특정 바이트를 추출하는 함수
    function byteExample(bytes1[] memory data, uint256 index) public pure returns (bytes1) {
        require(index < data.length, "Index out of bounds");
        return data[index];
    }
}
```

### 2단계: `OpcodeSample.sol` 컨트랙트 컴파일하기

### 3단계: `OpcodeSample.sol` 배포 후, 각 함수 호출하여 동작 확인하기

## 실습 4. 데이터 조회하기

> 실습 목표: 트랜잭션 호출 데이터와 블록 가스 한도를 조회하기 위해 CALLDATA, GAS LIMIT과 같은 블록 데이터 조회 옵코드를 사용하는 방법을 학습합니다.

이번 실습에서는 트랜잭션 호출 데이터(CALLDATA)와 블록 가스 한도(GAS LIMIT)를 조회하는 옵코드를 다루겠습니다.

### 1단계: `CALLDATA` 조회

`CALLDATA`는 트랜잭션과 함께 스마트 컨트랙트에 전송된 데이터를 의미합니다. `CALLDATALOAD` opcode를 사용하여 이 데이터를 로드할 수 있습니다.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract CallDataExample {
    event LogCallData(bytes data);

    function readCallData() public {
        bytes memory data = msg.data; // 전체 호출 데이터를 조회합니다.
        emit LogCallData(data);
    }

    function readSpecificData(uint256 index) public {
        bytes32 specificData;
        assembly {
            specificData := calldataload(index)
        }
        emit LogCallData(abi.encodePacked(specificData));
    }
}
```

**설명:**

- `msg.data`: 전체 호출 데이터를 조회합니다.
- `calldataload(index)`: 지정된 인덱스에서 32바이트의 호출 데이터를 조회합니다.

### 2단계: `GAS LIMIT` 조회

현재 블록의 가스 한도는 `GASLIMIT` 옵코드를 사용하여 접근할 수 있습니다.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract GasLimitExample {
    event LogGasLimit(uint256 gasLimit);

    function readGasLimit() public {
        uint256 gasLimit;
        assembly {
            gasLimit := gaslimit()
        }
        emit LogGasLimit(gasLimit);
    }
}
```

**설명:**

- `gaslimit()`: 현재 블록의 가스 한도를 조회합니다.

### 3단계: 컨트랙트 배포 및 테스트

1. **CallDataExample 배포:**
   - Remix 또는 선택한 IDE를 사용하여 `CallDataExample` 컨트랙트를 배포합니다.
   - 호출 데이터를 포함하여 `readCallData()` 함수를 호출하여 로그에 출력된 데이터를 확인합니다.
   - 인덱스를 지정하여 `readSpecificData(uint256 index)` 함수를 호출하여 특정 데이터가 로그에 출력되는 것을 확인합니다.
2. **GasLimitExample 배포:**
   - `GasLimitExample` 컨트랙트를 배포합니다.
   - `readGasLimit()` 함수를 호출하여 현재 블록의 가스 한도를 조회하고 로그에 출력합니다.

### 4단계: 테스트

- Remix IDE의 콘솔을 사용하여 다양한 호출 데이터를 포함한 트랜잭션을 전송하고 로그를 관찰합니다.
- 테스트넷을 사용하는 경우 블록체인 익스플로러를 통해, 로컬 블록체인을 사용하는 경우 Ganache를 통해 가스 한도를 모니터링합니다.

## 실습 5. 스토리지와 매핑하기

> 실습 목표: SLOAD를 통한 스테이트 변수(State Variable)의 저장과 읽기 방법을 이해하고, 슬롯 매핑을 통해 데이터를 저장하고 읽는 방법을 학습합니다.

### 1단계. Solidity 컨트랙트 작성

먼저 간단한 Solidity 컨트랙트를 작성하여 스테이트 변수를 정의하고, 이를 EVM 어셈블리를 통해 조작합니다.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract StorageMapping {
    uint256 public data;
    mapping(uint256 => uint256) public dataMap;

    // data 값을 설정하는 함수
    function setData(uint256 _data) public {
        data = _data;
    }

    // dataMap 값을 설정하는 함수
    function setDataMap(uint256 _key, uint256 _value) public {
        dataMap[_key] = _value;
    }

    // data 값을 가져오는 함수 (EVM 어셈블리 사용)
    function getData() public view returns (uint256) {
        uint256 value;
        assembly {
            value := sload(data.slot)
        }
        return value;
    }

    // dataMap 값을 가져오는 함수 (EVM 어셈블리 사용)
    function getDataMap(uint256 _key) public view returns (uint256) {
        uint256 slot;
        uint256 value;
        assembly {
            // 해시 함수 keccak256을 사용하여 mapping의 슬롯 계산
            mstore(0x0, _key)
            mstore(0x20, dataMap.slot)
            slot := keccak256(0x0, 0x40)
            value := sload(slot)
        }
        return value;
    }
}
```

**설명:**

1. **컨트랙트 정의 및 스테이트 변수 설정**:
   - `data`와 `dataMap`이라는 두 개의 스테이트 변수를 정의합니다.
   - `setData` 및 `setDataMap` 함수는 각각 `data`와 `dataMap` 값을 설정합니다.
2. **EVM 어셈블리를 사용하여 데이터 읽기**:
   - `getData` 함수는 EVM 어셈블리를 사용하여 `data`의 값을 읽습니다. 여기서 `sload(data.slot)` 명령어를 사용하여 `data`의 슬롯에서 값을 가져옵니다.
   - `getDataMap` 함수는 주어진 키를 기반으로 `dataMap`의 값을 읽습니다. 매핑된 값은 `keccak256` 해시 함수를 사용하여 슬롯을 계산하고, 해당 슬롯에서 값을 읽습니다.

### 2단계: 테스트

다음은 이 컨트랙트를 테스트하는 방법입니다

1. **설정 함수 호출**:
   - `setData(42)`를 호출하여 `data` 값을 42로 설정합니다.
   - `setDataMap(1, 100)`을 호출하여 키 1에 대해 `dataMap` 값을 100으로 설정합니다.
2. **가져오기 함수 호출**:
   - `getData()`를 호출하여 `data` 값을 가져옵니다. 반환값은 42가 되어야 합니다.
   - `getDataMap(1)`을 호출하여 키 1에 대한 `dataMap` 값을 가져옵니다. 반환값은 100이 되어야 합니다.

## 나가며

~~ TODO: 위 내용 다 작성되면 마무리 멘트 작성이 필요함
